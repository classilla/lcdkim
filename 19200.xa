
	* = $1780

	; max 102 bytes
	;
	; send a character using PA0 as a bitbanged serial port at 19200bps
	; (pin A-14)
	;
	; (c)2023 cameron kaiser. all rights reserved. bsd license.

zp	= $f9
yp	= zp+2

ch	= $fe

	; 19200 bps on 1.000000MHz 6502 is 52.1 cycles/bit
	; (i.e., 1_000_000/19_200)

send	sei		; paranoia
	sta ch

	lda $1701
	ora #1
	sta $1701	; pa0 to output

	ldx #8

	; start bit
	lda $1700	; (4)
	and #$fe	; (2)
	sta $1700	; (4) bit live

	ldy #5
	jsr delayy	; 38
	
send8	; cribbed from kim-1 outch routine $1ea0
	and #$fe	; 2
	lsr ch		; 5
	adc #0		; 2
	sta $1700	; 4 - bit live

	ldy #4
	jsr delayy	; 33

	dex		; 2 
	bne send8	; if taken 3 if not taken 2
	; 4 or 5

	; stop bit
sends	lda $1700	; 4
	ora #$01	; 2
	nop		; 2
	nop		; 2
	sta $1700	; 4 - bit live

	ldy #7
	jsr delayy	; 48 (safest)
	cli
	rts

	; plus 12 jsr and rts are 12 cycles combined
	; so cycles = 2 + y*2 + (y-1)*3 + 2
delayy	dey
	bne delayy	; each loop taken = 3, final branch = 2
	rts

#ifdef XA_MAJOR
.assert *<$17e6, "routine too long"
#endif

